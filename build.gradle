buildscript {
	ext.kotlin_version = '1.2.31'

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'

group = 'org.javacs'
version = '0.1.1'

mainClassName = "org.javacs.kt.MainKt"
description = "kotlin-language-server"

sourceCompatibility = 1.8
targetCompatibility = 1.8

kotlin {
	experimental {
		coroutines 'enable'
	}
}

repositories {
	maven { url "http://repo.maven.apache.org/maven2" }
	maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
	compile "org.gradle:gradle-tooling-api:4.3"

	// FIXME: Strangely enough tests won't find Guava classes when only LSP4J-Guava is bundled
	// (whilst the application will run fine) and the application won't find Guava when
	// it is listed explicitly below (whilst the tests will run fine). This even happens
	// when the exact same version of Guava is explicitly listed as required by LSP4J...

	// compile 'com.google.guava:guava:[14.0,22) -> 21.0'
	compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.4.1'
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.2.31'
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-compiler', version: '1.2.31'
	testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.20'
	testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
	testCompile group: 'junit', name: 'junit', version: '4.10'
	compileOnly group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.20'
}

jar {
	archiveName = 'KotlinLanguageServer.jar'

	manifest {
		attributes 'Main-Class': mainClassName
	}

	// Include dependencies in JAR
	from (configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude 'META-INF/MANIFEST.MF'
		exclude 'META-INF/*.SF'
		exclude 'META-INF/*.DSA'
		exclude 'META-INF/*.RSA'
	}
}

task printDependencies {
	doLast {
		configurations.runtime.resolve().each {
			println it.getAbsolutePath()
		}
	}
}
